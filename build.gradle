import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

wrapper {
    gradleVersion = '4.8.1'
    distributionType = 'all'
}

group = project.pluginGroup
version = project.pluginVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'


[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

assemble.dependsOn clean

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    implementation group: 'org.spigotmc', name: 'spigot-api', version: '1.12.2-R0.1-SNAPSHOT'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '2.0.2-beta'
}

test {
    useJUnitPlatform()
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filteringCharset = 'UTF-8'
        filter ReplaceTokens, tokens: [version: project.version]
    }
}

shadowJar {
    classifier = ''
}

artifacts {
    archives jar
}

task prepareUpload(type: Copy, dependsOn: ['build']) {
    from "$buildDir/libs"
    into "$buildDir/libs"
    include "rattranslate-${project.version}.jar"
    rename "rattranslate-${project.version}.jar", 'RatTranslate.jar'
}
